
Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ОстаткиНоменклатур.Записывать = Истина;
	Движение = Движения.ОстаткиНоменклатур.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Период = Дата;
	Движение.Номенклатура = ЭтотОбъект.Товар;
	Движение.Количество = ЭтотОбъект.Количество;
	
	Для Каждого ТекСтрокаОстатки Из Остатки Цикл
		Движение = Движения.ОстаткиНоменклатур.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Номенклатура = ТекСтрокаОстатки.Ингредиент;
		Движение.Количество = МодульКонвертированияЕдиницИзмерения.ВСтандартнуюЕИКонвертировать(
			ТекСтрокаОстатки.ЕдиницаИзмерения, ТекСтрокаОстатки.Количество);
	КонецЦикла;
	
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(РегистрОстатковОстатки.Номенклатура) КАК НоменклатураНаименование,
		|	РегистрОстатковОстатки.Номенклатура КАК НоменклатураСсылка,
		|	ЕСТЬNULL(СправочникНоменклатура.ВидЕдиницыИзмерения, СправочникТовары.ВидЕдиницыИзмерения) КАК ВидЕдиницыИзмерения,
		|	РегистрОстатковОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатур.Остатки(&Дата, Номенклатура В (&Остатки)) КАК РегистрОстатковОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО
		|	СправочникНоменклатура.Ссылка = РегистрОстатковОстатки.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ
		|	Справочник.Товары КАК СправочникТовары
		|		ПО
		|	СправочникТовары.Ссылка = РегистрОстатковОстатки.Номенклатура
		|ГДЕ
		|	РегистрОстатковОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("Дата", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Остатки", Остатки.ВыгрузитьКолонку("Ингредиент"));
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
		
		Пока Выборка.Следующий() Цикл
			Сообщить("Произошла ошибка: Количество ингредиента " + Выборка.НоменклатураНаименование + " недостаточно" +
				" для проведения документа, необходимо еще " + -(Выборка.КоличествоОстаток) + " " + 
				ПараметрыСеанса.СтандартныеЕдиницыИзмерения[Выборка.ВидЕдиницыИзмерения]);
		КонецЦикла;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Движения.РасходыИПродажиНаТовары.Записывать = Истина;
	Движение = Движения.РасходыИПродажиНаТовары.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
	Движение.Период = Дата;
	Движение.Товар = Товар;
	СуммаТоваров = 0;
	
	ЗапросТоваров = Новый Запрос;
	ЗапросТоваров.Текст = "ВЫБРАТЬ
	|	ЦеныПокупкиПродажиСрезПоследних.Цена КАК Цена,
	|	ЕСТЬNULL(Остатки.Количество, 0) КАК Количество
	|ИЗ
	|	РегистрСведений.ЦеныПокупкиПродажи.СрезПоследних(&ДатаСреза) КАК ЦеныПокупкиПродажиСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|	Документ.РеализацияАссортимента.Остатки КАК Остатки
	|		ПО
	|	Остатки.Ингредиент = ЦеныПокупкиПродажиСрезПоследних.Номенклатура
	|ГДЕ
	|	ЦеныПокупкиПродажиСрезПоследних.Номенклатура В (&Номенклатуры) И
	|	Остатки.Ссылка = &Документ";
	
	ЗапросТоваров.УстановитьПараметр("Номенклатуры", Остатки.ВыгрузитьКолонку("Ингредиент"));
	ЗапросТоваров.УстановитьПараметр("ДатаСреза", Дата);
	ЗапросТоваров.УстановитьПараметр("Документ", ЭтотОбъект.Ссылка);
	
	РезультатЗапросаТоваров = ЗапросТоваров.Выполнить();
	
	Если Не РезультатЗапросаТоваров.Пустой() Тогда
		ВыборкаТоваров = РезультатЗапросаТоваров.Выбрать();
		
		Пока ВыборкаТоваров.Следующий() Цикл
			СуммаТоваров = СуммаТоваров + (ВыборкаТоваров.Цена * ВыборкаТоваров.Количество)
		КонецЦикла;
	КонецЕсли;
	
	Движение.Сумма = СуммаТоваров;
	
КонецПроцедуры


Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

