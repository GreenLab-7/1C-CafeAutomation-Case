
Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ОстаткиНоменклатур.Записывать = Истина;
	Для Каждого ТекСтрокаТовары Из Товары Цикл
		Движение = Движения.ОстаткиНоменклатур.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Номенклатура = ТекСтрокаТовары.Товары;
		Движение.Количество = ТекСтрокаТовары.Количество;
	КонецЦикла;
	
	Движения.Записать();
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РегистрОстатковОстатки.Номенклатура.Наименование КАК НоменклатураНаименование,
		|	РегистрОстатковОстатки.КоличествоОстаток КАК КоличествоОстаток
		|ИЗ
		|	РегистрНакопления.ОстаткиНоменклатур.Остатки(&Дата, Номенклатура В (&Товары)) КАК РегистрОстатковОстатки
		|ГДЕ
		|	РегистрОстатковОстатки.КоличествоОстаток < 0";
	
	Запрос.УстановитьПараметр("Дата", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Товары", Товары.ВыгрузитьКолонку("Товары"));
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Отказ = Истина;
		
		Пока Выборка.Следующий() Цикл
			Сообщить("Произошла ошибка: Количество товара " + Выборка.НоменклатураНаименование + " недостаточно" +
				" для проведения документа, необходимо еще " + -(Выборка.КоличествоОстаток));
		КонецЦикла;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ПриводитсяКУчетуПродаж Тогда
		
		Движения.НеучтенныеПродажи.Записывать = Истина;
		Для Каждого ТекСтрокаТовары Из Товары Цикл
			Движение = Движения.НеучтенныеПродажи.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Товар = ТекСтрокаТовары.Товары;
			Движение.Количество = ТекСтрокаТовары.Количество;
		КонецЦикла;
	
		Движения.Записать();
		
	КонецЕсли;
	
КонецПроцедуры
